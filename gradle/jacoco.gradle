// This file is provided by ARC-E-TECT (Arc-E-Tect.medium.com)
// and provided under the Creative Commons - CC BY-SA 4.0 license
// You can share this code, change it as you see fit and even make money from it,
// as long as you agree to share under the same circumstance.
// See: https://creativecommons.org/licenses/by-sa/4.0/

import static de.fayard.refreshVersions.core.Versions.versionFor

apply plugin: 'java'
apply plugin: 'jacoco'

// -----------------------------------------------------------
//
// JaCoCo
dependencies {
    implementation "org.jacoco:org.jacoco.agent:_:runtime"
}

jacoco {
    toolVersion = versionFor(project, "version.org.jacoco..org.jacoco.agent")
}

jacocoTestReport {
    sourceSets sourceSets.main    // important!
    executionData fileTree(project.buildDir.absolutePath).include("jacoco/*.exec")
    reports {
        xml.required = false
        csv.required = true
        html.destination file("${buildDir}/reports/jacoco/Html")
        csv.destination file("${buildDir}/reports/jacoco/jacoco.csv")
    }

    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            '**/com/arc_e_tect/blog/phonebook/ArcETectPhonebookApplication.class',
                            '**/com/arc_e_tect/blog/phonebook/**/exception/**.class'
                    ]
            )
        })
    }
}

jacocoTestCoverageVerification {
    executionData fileTree(project.buildDir.absolutePath).include("jacoco/*.exec")
    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            '**/com/arc_e_tect/blog/phonebook/ArcETectPhonebookApplication.class',
                            '**/com/arc_e_tect/blog/phonebook/**/exception/**.class'
                    ]
            )
        })
    }
    violationRules {
        rule {
            limit {
                minimum = minCodeCoverage
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
