buildscript {
    ext {
        minCodeCoverage = 1.0
    }

    repositories {
        mavenCentral()
    }

    apply from: "dependency_version.properties"

    dependencies {
        classpath  group: "org.owasp", name:"dependency-check-gradle", version:"${dependencyCheckPluginVersion}"
    }

    apply from: 'gradle/jacoco.gradle'
}

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"

    // Java developers should always use Lombok to make their code less cluttered.
    id "io.freefair.lombok" version "${lombokGradleVersion}"

    // Documentation is best generated with every build.
    id "org.asciidoctor.jvm.convert" version "${asciiDoctorGradleVersion}"
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecGradleVersion}"

    // Being able to continuously scan whether there are new versions of your dependencies will simplify your life
    id "com.github.ben-manes.versions" version "${gitversionerGradleVersion}"

    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "${cucumberReportingGradleVersion}"
    id "com.github.jmongard.git-semver-plugin" version "${gitSemverPluginVersion}"

    // Used to have continuous vulnerability scanning for my dependencies.
    // Simple way to eliminate the risk of Log4Shell shock
    id "org.owasp.dependencycheck" version "${dependencyCheckPluginVersion}"

    // Knowing what licenses the dependencies of your application have and fail the build when you violate a compliance
    // rule during development time saves a lot of time and money from lawsuits
    id "com.cmgapps.licenses" version "${licenseChecker}"
    id 'org.unbroken-dome.test-sets' version "${testSetsGradleVersion}"

    // Running MongoDB in a Docker container simplifies development and therefore speeds it up
    id 'com.avast.gradle.docker-compose' version "$avastDockerComposeGradleVersion"

    // Having all dependencies in a standard format available will make it easier for your legal department to define
    // contracts that can be uphold from day one.
    id "org.cyclonedx.bom" version "${cycloneDxVersion}"
    id "nebula.lint" version "${gradleLintVersion}"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

apply from: 'gradle/dependencyupdates.gradle'

apply from: 'gradle/semver.gradle'

// -----------------------------------------------------------
//
// Property generation and substitution
ext['junit-jupiter.version'] = "${jUnitVersion}" // Also change jUnitVersion
def repositoryDir = rootProject.buildDir.path + File.separator + "repository"
mkdir repositoryDir
ext['repositoryDir'] = new File(repositoryDir).toURI()
ext['projectName'] = project.name

processResources {
    def props = ["projectName":project.name, "versionName":version, "buildNumber":buildNumber]
    filesMatching (['**/application.properties', '**/extent-config.xml']) {
        expand project.properties
    }
}

lombok {
    version = "${lombokVersion}"
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: "${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas', version: "${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: "${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: "${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb', version: "${springBootVersion}"

    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: "${springDocVersion}"
    testImplementation (group: 'org.springframework.boot',  name: 'spring-boot-starter-test',   version: "${springBootVersion}") {
        exclude group: "junit"
    }

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation group: "com.google.flogger", name: "flogger-system-backend", version: "${floggerVersion}"
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

testSets {
    bddTest {
        dirName = 'bdd-test'
    }
    docGen {
        dirName = 'docs'
    }
}

bddTest.dependsOn test
docGen.mustRunAfter bddTest
check.dependsOn bddTest, docGen

jacocoTestReport {
    dependsOn bddTest, docGen
}

jacocoTestCoverageVerification {
    dependsOn test, bddTest, docGen
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    testLogging.showStandardStreams = true
    reports {
        junitXml.required = true
        html.required = true
    }

    finalizedBy jacocoTestReport
}

// -----------------------------------------------------------
//
// JUnit
apply from: 'gradle/junit.gradle'

// -----------------------------------------------------------
//
// Cucumber
apply from: 'gradle/cucumber.gradle'

// -----------------------------------------------------------
//
// Security
apply from: 'gradle/security.gradle'

// -----------------------------------------------------------
//
// ASCII Doctor
apply from: 'gradle/asciidoctor.gradle'

bootJar {
    dependsOn asciidoctor
    mainClass  = 'com.arc_e_tect.blog.phonebook.ArcETectPhonebookApplication'
    from ("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
    from ("${cucumberReports.outputDir}/cucumber-html-reports") {
        into 'static/reports/scenarios'
    }
    from ("${buildDir}/api-spec") {
        into 'static/specs'
    }
}

springBoot {
    buildInfo {
        properties {
            name = "Acr-E-Tect Phonebook"
            additional = [
                    by: System.properties['user.name'],
                    continuousIntegration: System.getenv('CI') ? true: false,
            ]
        }
    }
}

// -----------------------------------------------------------
//
// RestDoc API Spec
dependencies {
    testImplementation group: 'com.epages', name: 'restdocs-api-spec-mockmvc', version: "${restdocsApiSpecGradleVersion}"
}

openapi3 {
    server = 'https://localhost:9000'
    title = 'Arc-E-Tect Phonebook API'
    description = 'API description for the Arc-E-Tect Phonebook service'
    version = '1.0'
    format = 'json'
}

postman {
    title = 'Arc-E-Tect Phonebook API'
    version = "${version}"
    baseUrl = 'https://localhost:9000'
}

// ------------------------------------------------------------
// Docker

dockerCompose.isRequiredBy(bddTest)
dockerCompose.isRequiredBy(docGen)
dockerCompose.isRequiredBy(bootRun)

dockerCompose.stopContainers=false

gradleLint {
    autoLintAfterFailure="false"
    rules  = ['archaic-wrapper']
}
