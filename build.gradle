buildscript {
    ext {
        minCodeCoverage = 0.80
    }

    repositories {
        mavenCentral()
        jcenter() // for gradle-versions-plugin
    }

    dependencies {
        classpath 'org.owasp:dependency-check-gradle:6.1.2'
    }

    apply from: 'dependency_version.properties'
    apply from: 'gradle/jacoco.gradle'
}

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'application'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "${cucumberReportingGradleVersion}"
    id "com.github.ben-manes.versions" version "${gitversionerGradleVersion}"
    id "io.freefair.lombok" version "${lombokGradleVersion}"
    id "com.github.jmongard.git-semver-plugin" version "${gitSemverPluginVersion}"
    id 'name.remal.sonarlint' version "${sonarLintPluginVersion}"
    id "org.owasp.dependencycheck" version "${dependencyVulnerabilityChecker}"

    id "com.cmgapps.licenses" version "${licenseChecker}"
}

group = 'com.arc_e_tect.blog.phonebook'
sourceCompatibility = '11'
targetCompatibility = '11'

dependencyUpdates.gradleReleaseChannel="current"

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

repositories {
    jcenter()
}

lombok {
    version = "${lombokVersion}"
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

jacocoTestReport {
    dependsOn test
}

application {
    mainClassName = 'com.arc_e_tect.blog.phonebook.PhonebookApplication'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    testLogging.showStandardStreams = true
    reports {
        junitXml.enabled = true
        html.enabled = true
    }

    finalizedBy jacocoTestReport
}

// -----------------------------------------------------------
//
// JUnit
apply from: 'gradle/junit.gradle'

// -----------------------------------------------------------
//
// Cucumber
apply from: 'gradle/cucumber.gradle'

// -----------------------------------------------------------
//
// Cucumber
apply from: 'gradle/sonarlint.gradle'

