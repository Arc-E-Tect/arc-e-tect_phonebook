buildscript {
    apply from: 'dependency_version.properties'
}

plugins {
    id 'java'
    id 'idea'
    id 'application'
    id "com.github.ben-manes.versions" version "${gitversionerGradleVersion}"
}

apply plugin: 'idea'

repositories {
    jcenter()
}

dependencies {
    testImplementation  group: 'org.junit.jupiter',     name: 'junit-jupiter-api',      version: "${jUnitVersion}"
    testRuntimeOnly     group: 'org.junit.jupiter',     name: 'junit-jupiter-engine',   version: "${jUnitVersion}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

application {
    mainClassName = 'com.arc_e_tect.blog.phonebook.PhonebookApplication'
}

test {
    useJUnitPlatform()

    finalizedBy ("jacocoTestReport")
}

/**********************************************************************************
 JaCoCo Support
 **********************************************************************************/

apply plugin: "jacoco"
jacoco {
    toolVersion = "${jacocoToolVersion}"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/reports/jacocoHtml")
    }
}

test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        includes= ["com.arc_e_tect.*"]
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
}

/***************************************************************************************************
 * Cucumber Support
 ***************************************************************************************************/

dependencies {
    testImplementation group: "io.cucumber",    name: "cucumber-java",                  version: "${cucumberVersion}"
    testImplementation group: "io.cucumber",    name: "cucumber-junit",                 version: "${cucumberVersion}"
    testImplementation group: 'io.cucumber',    name: 'cucumber-junit-platform-engine', version: "${cucumberVersion}"
}

